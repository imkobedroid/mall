if (isUserModule.toBoolean()) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: "../version.gradle"

kapt {
    arguments {
        arg("moduleName", project.getName())
    }
}

android {
    compileSdkVersion LyVersion.compileSdkVersion
    buildToolsVersion LyVersion.buildToolsVersion

    defaultConfig {
        minSdkVersion LyVersion.minSdkVersion
        targetSdkVersion LyVersion.targetSdkVersion
        versionCode LyVersion.versionCode
        versionName LyVersion.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //动态的切换manifest
    sourceSets {
        main {
            if (isUserModule.toBoolean()) {
                manifest.srcFile 'src/main/release/AndroidManifest.xml'

            } else {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'

            }
        }
    }


}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    api project(':Ly-BASE-Provider')
    api LyCommonDependencies["dagger2"]
    kapt LyCommonDependencies["daggerCompiler"]

}
repositories {
    mavenCentral()
}

